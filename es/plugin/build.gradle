buildscript {
    ext {
        esVersion = '7.9.0'
        grpcVersion = '1.31.1'
        protobufVersion = '3.12.0'
        protocVersion = protobufVersion
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${esVersion}"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.13'
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "com.google.protobuf"
apply plugin: "elasticsearch.esplugin"

sourceCompatibility = 14
targetCompatibility = 14

repositories {
    mavenCentral()
    jcenter()
}


dependencies {
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "com.google.protobuf:protobuf-java:${protobufVersion}"
    compile 'info.picocli:picocli:4.5.0'
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
    compile group: 'org.elasticsearch', name: 'elasticsearch', version: "${esVersion}"

    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins {grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

esplugin {
    name 'ml-grpc-rescore'
    description 'Rescoring plugin for external grpc model'
    classname 'com.accretivetg.MLGrpcRescoringPlugin'
    licenseFile rootProject.file("LICENSE.txt")
    noticeFile rootProject.file("NOTICE.txt")
}

checkstyle {
    sourceSets = []
}

tasks.withType(Javadoc).all { enabled = false }
