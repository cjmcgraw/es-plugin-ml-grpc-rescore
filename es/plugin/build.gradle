buildscript {
    ext {
        esVersion = '7.9.0'
        grpcVersion = '1.31.1'
        protobufVersion = '3.12.0'
        protocVersion = protobufVersion
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${esVersion}"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.13'
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "com.google.protobuf"
apply plugin: "elasticsearch.esplugin"

sourceCompatibility = 14
targetCompatibility = 14

repositories {
    mavenCentral()
    jcenter()
}


dependencies {
    configurations.all {
        resolutionStrategy.force 'com.google.errorprone:error_prone_annotations:2.4.0'
    }

    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "com.google.protobuf:protobuf-java:${protobufVersion}"
    compile 'info.picocli:picocli:4.5.0'
    compile "org.apache.tomcat:annotations-api:6.0.53"
    compile "org.elasticsearch:elasticsearch:${esVersion}"
    compile "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    compile "io.grpc:grpc-netty-shaded:${grpcVersion}"
    compile group: 'com.googlecode.protobuf-java-format', name: 'protobuf-java-format', version: '1.4'
    compile 'com.github.ben-manes.caffeine:caffeine:2.8.5'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins {grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

esplugin {
    name 'ml-grpc-rescore'
    description 'Rescoring plugin for external grpc model'
    classname 'qarlm.esplugin.MLGrpcRescoringPlugin'
    licenseFile rootProject.file("LICENSE.txt")
    noticeFile rootProject.file("NOTICE.txt")
}
checkstyleMain.onlyIf { false }
checkstyleTest.onlyIf { false }
javadoc.onlyIf { false }